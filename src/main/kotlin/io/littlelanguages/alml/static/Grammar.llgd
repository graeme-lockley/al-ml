uses "./Scanner.llld";

Program: Expressions;

Expressions: Expression {";" Expression};

Expression
  : "{" Expression {";" Expression} "}"
  | "let" Symbol {Symbol} "=" Expression
  | IfExpression
  ;

IfExpression
  : "if" Expression "->" Expression {"|" Expression "->" Expression} ["else" Expression]
  | RelationalExpression
  ;

RelationalExpression
  : AdditiveExpression [RelationalOp AdditiveExpression]
  ;

RelationalOp: "==" | "!=" | "<" | "<=" | ">" | ">=";

AdditiveExpression
  : MultiplicativeExpression {("+" | "-") MultiplicativeExpression}
  ;

MultiplicativeExpression
  : Term {("*" | "/") Term}
  ;

Term
  : "(" [ExpressionBody] ")"
  | LiteralInt
  | LiteralString
  | Symbol
  ;

ExpressionBody
  : "proc" "(" {Symbol} ")" {Expression}
  | "try" Expression Expression
  | "signal" Expression
  | Expression {Expression}
  ;
